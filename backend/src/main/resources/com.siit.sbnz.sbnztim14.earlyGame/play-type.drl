
package com.siit.sbnz.sbnztim14.earlyGame

import com.siit.sbnz.sbnztim14.model.Champion;
import com.siit.sbnz.sbnztim14.model.AllyChampion;
import com.siit.sbnz.sbnztim14.model.EnemyChampion;
import com.siit.sbnz.sbnztim14.model.JungleEarlyPlayType;
import com.siit.sbnz.sbnztim14.model.AdviceStorage;

global com.siit.sbnz.sbnztim14.model.AdviceStorage adviceStorage;

rule "Ally countered by enemy"
    agenda-group "play-type"
    lock-on-active
    when
        $ally : AllyChampion(
            $allyLane: lane,
            $allyName: name,
            $allyBadAgainst: badAgainst,
            lane != "jungle"
        )
        $enemy : EnemyChampion(
            lane == $allyLane,
            goodAgainst contains $allyName || name memberOf $allyBadAgainst
        )
    then
        modify($ally){setPlayType("def")}
end

rule "Ally strong early, enemy weak early, not countered"
    agenda-group "play-type"
    lock-on-active
    when
        $ally : AllyChampion(
            $allyLane: lane,
            $allyName: name,
            $allyBadAgainst: badAgainst,
            lane != "jungle",
            goodEarly == true
        )
        $enemy : EnemyChampion(
            lane == $allyLane,
            goodEarly == false,
            goodAgainst not contains $allyName,
            name not memberOf $allyBadAgainst
        )
    then
        modify($ally){setPlayType("aggro")}
end

rule "Ally weak early, enemy strong early, not countered"
    agenda-group "play-type"
    lock-on-active
    when
        $ally : AllyChampion(
            $allyLane: lane,
            $allyName: name,
            $allyBadAgainst: badAgainst,
            lane != "jungle",
            goodEarly == false
        )
        $enemy : EnemyChampion(
            lane == $allyLane,
            goodEarly == true,
            goodAgainst not contains $allyName,
            name not memberOf $allyBadAgainst
        )
    then
        modify($ally){setPlayType("def")}
end


// Ally will start with playType which he/she prefers
rule "Ally strong early, enemy strong early, not countered"
    agenda-group "play-type"
    lock-on-active
    when
        $ally : AllyChampion(
            $allyLane: lane,
            $allyName: name,
            $allyBadAgainst: badAgainst,
            lane != "jungle",
            goodEarly == true,
            $prefer: playType
        )
        $enemy : EnemyChampion(
            lane == $allyLane,
            goodEarly == true,
            goodAgainst not contains $allyName,
            name not memberOf $allyBadAgainst
        )
    then
        modify($ally){setPlayType($prefer)}
end


// Ally will start with playType which he/she prefers
rule "Ally weak early, enemy weak early, not countered"
    agenda-group "play-type"
    lock-on-active
    when
        $ally : AllyChampion(
            $allyLane: lane,
            $allyName: name,
            $allyBadAgainst: badAgainst,
            lane != "jungle",
            goodEarly == false,
            $prefer: playType
        )
        $enemy : EnemyChampion(
            lane == $allyLane,
            goodEarly == false,
            goodAgainst not contains $allyName,
            name not memberOf $allyBadAgainst
        )
    then
        modify($ally){setPlayType($prefer)}
end

