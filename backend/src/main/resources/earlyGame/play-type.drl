package earlyGame

import com.siit.sbnz.sbnztim14.model.Champion;
import com.siit.sbnz.sbnztim14.model.AllyChampion;
import com.siit.sbnz.sbnztim14.model.EnemyChampion;
import com.siit.sbnz.sbnztim14.model.JungleEarlyPlayType;
import com.siit.sbnz.sbnztim14.model.AdviceStorage;

global com.siit.sbnz.sbnztim14.model.AdviceStorage adviceStorage;

// non-jungle

rule "Ally strong early, not countered"
	when
		$ally : AllyChampion(
			$allyLane: lane,
			$allyName: name,
			$allyBadAgainst: badAgainst,
			lane != "jungle",
			goodEarly == true
		)
		$enemy : EnemyChampion(
			lane == $allyLane,
			goodAgainst not contains $allyName,
			name not memberOf $allyBadAgainst
		)
	then
		modify($ally){setPlayType("aggro")}
end

rule "Ally strong early, but countered"
	when
		$ally : AllyChampion(
			$allyLane: lane,
			$allyName: name,
			$allyBadAgainst: badAgainst,
			lane != "jungle",
			goodEarly == true
		)
		$enemy : EnemyChampion(
			lane == $allyLane,
			goodAgainst contains $allyName || name memberOf $allyBadAgainst
		)
	then
		modify($ally){setPlayType("def")}
end

rule "Ally weak early, enemy strong early"
	when
		$ally : AllyChampion(
			$allyLane: lane,
			lane != "jungle",
			goodEarly == false
		)
		$enemy : EnemyChampion(
			lane == $allyLane,
			goodEarly == true
		)
	then
		modify($ally){setPlayType("def")}
end

rule "Ally weak early, countered"
	when
		$ally : AllyChampion(
			$allyLane: lane,
			$allyName: name,
			$allyBadAgainst: badAgainst,
			lane != "jungle",
			goodEarly == false
		)
		$enemy : EnemyChampion(
			lane == $allyLane,
			goodAgainst contains $allyName || name memberOf $allyBadAgainst
		)
	then
		modify($ally){setPlayType("def")}
end

rule "Ally weak early, but not countered and enemy weak early"
	when
		$ally : AllyChampion(
			$allyLane: lane,
			$allyName: name,
			$allyBadAgainst: badAgainst,
			lane != "jungle",
			goodEarly == false
		)
		$enemy : EnemyChampion(
			lane == $allyLane,
			goodEarly == false,
			goodAgainst not contains $allyName,
			name not memberOf $allyBadAgainst
		)
	then
		modify($ally){setPlayType("aggro")}
end

// jungle

rule "Jungle, 1v1 lane, laner aggro"
	when
		$ally : AllyChampion(
			$allyLane: lane,
			lane == "top" || lane == "mid",
			playType == "aggro"
		)
	then
		insert(new JungleEarlyPlayType($allyLane, "aggro"));

end

rule "Jungle, 1v1 lane, laner def, jungle strong early"
	when
		$ally : AllyChampion(
			$allyLane: lane,
			lane == "top" || lane == "mid",
			playType == "def"
		)
		$jungler : AllyChampion(
			lane == "jungle",
			goodEarly == true
		)
	then
		insert(new JungleEarlyPlayType($allyLane, "aggro"));

end

rule "Jungle, 1v1 lane, laner def, jungle weak early"
	when
		$ally : AllyChampion(
			$allyLane: lane,
			lane == "top" || lane == "mid",
			playType == "def"
		)
		$jungler : AllyChampion(
			lane == "jungle",
			goodEarly == false
		)
	then
		insert(new JungleEarlyPlayType($allyLane, "def"));

end


